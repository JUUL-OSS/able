buildscript {
    apply from: rootProject.file('gradle/dependencies.gradle')

    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version '1.3.70' apply false
    id 'org.jmailen.kotlinter' version '2.2.0' apply false
    id 'com.android.library' version '3.6.2' apply false
    id 'com.vanniktech.maven.publish' version '0.11.1' apply false

    // `jacoco-android` is incompatible with Gradle 6.x, fails with:
    // groovy.lang.GroovyRuntimeException: Cannot set the value of read-only property 'executionData'...
    // https://github.com/arturdm/jacoco-android-gradle-plugin/issues/72
    id 'com.hiya.jacoco-android' version '0.2' apply false

    id 'binary-compatibility-validator' version '0.2.3'
}

subprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}

// Prevent publishing if `VERSION_NAME` property is invalid (i.e. enforce `-PVERSION_NAME` command
// line argument).
gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().findAll { task ->
        task.name.startsWith('installArchives') || task.name.startsWith('publishArchives')
    }.forEach { task ->
        task.doFirst {
            if (!project.hasProperty("VERSION_NAME") || project.findProperty("VERSION_NAME").startsWith("unspecified")) {
                logger.error("VERSION_NAME=" + project.findProperty("VERSION_NAME"))
                throw new GradleException("Unable to publish without valid VERSION_NAME property")
            }
        }
    }
}
